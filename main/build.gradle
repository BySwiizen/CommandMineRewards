import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java-library'
}

version = "7.2.1"

dependencies {
    implementation project(':api-interfaces')
    implementation project(':legacy-hooks')
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    // Most recent version of WG compiled with Java 8
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.3'
    compileOnly 'org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT'
    // 2.1.202 was the last version to use Java 8
    compileOnly 'com.gmail.nossr50.mcMMO:mcMMO:2.1.202'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
}

shadowJar {
    minimize()
    def path = 'me.datatags.commandminerewards.shaded'
    relocate ('org.bstats', path + '.bstats')
    
    archiveFileName = "CommandMineRewards-v${archiveVersion.get()}.${archiveExtension.get()}"
}

processResources {
    inputs.property("version", project.version)

    filter ReplaceTokens, tokens: [
            'VERSION': project.version
    ]
}
